struct SDFractal
{
	float Iterations;
	float Bailout;
	float Power;
	float FoldingScale;
	float Scale;
	float OrbitType;
	float Offset;
	float3 FoldAngles;
	float3 FoldOffset;
	float3 FoldSize;
	// Fractal configuration

	float Time;
	// Animation

	float3 CameraVector;

	float3 Orbit;
	float3 OrbitOrigin;
	// Orbit Coloring

	void initialize(float _Iterations, float _Bailout, float _Power, 
	float _FoldingScale,
	float3 _CameraVector, float _Scale, float3 _Orbit, float _OrbitType,
	float3 _OrbitOrigin, float3 _FoldAngles, float3 _FoldOffset,
	float3 _FoldSize, float _Offset, float _Time)
	{
		Iterations = _Iterations;
		Bailout = _Bailout;
		Power = _Power;
		FoldingScale = _FoldingScale;
		Scale = _Scale;
		OrbitType = _OrbitType;
		seed = 1.0f;
		Time = _Time;
		CameraVector = _CameraVector;
		Orbit = _Orbit;
		OrbitOrigin = _OrbitOrigin;
		FoldAngles = _FoldAngles;
		FoldOffset = _FoldOffset;
		FoldSize = _FoldSize;
		Offset = _Offset;
	}

	float3 orbitByType(float3 orbit, float4 p)
	{
		if (OrbitType == 0.0f) orbit = orbitMin(orbit, p.xyz, Orbit , OrbitOrigin);
		else if (OrbitType == 1.0f) orbit = orbitMinAbs(orbit, p.xyz, Orbit , OrbitOrigin);
		else if (OrbitType == 2.0f) orbit = orbitMax(orbit, p.xyz, Orbit , OrbitOrigin);
		else if (OrbitType == 3.0f) orbit = orbitMaxAbs(orbit, p.xyz, Orbit , OrbitOrigin);
		else if (OrbitType == 4.0f) orbit = orbitSum(orbit, p.xyz, Orbit , OrbitOrigin);
		else orbit = orbitSumAbs(orbit, p.xyz, Orbit , OrbitOrigin);

		return orbit;
	}

	float rand(float s, float minV, float maxV) {
		float r = sin(s*s*27.12345 + 1000.9876 / (s*s + 1e-5));
		return (r + 1.0) * 0.5 * (maxV - minV) + minV;
	}
	float smin(float a, float b, float k) {
		float h = clamp(0.5 + 0.5*(b-a)/k, 0.0, 1.0 );
		return lerp(b, a, h) - k*h*(1.0 - h);
		//return -log(exp(-a/k) + exp(-b/k))*k;
	}

	//##########################################
	//   Orbit functions
	float3 orbitInitZero()
	{
		return float3(0.0f, 0.0f, 0.0f);
	}

	float3 orbitInitInf()
	{
		return float3(1e20, 1e20, 1e20);
	}

	float3 orbitInitNegInf()
	{
		return float3(-1e20, -1e20, -1e20);
	}

	float3 orbitMin(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return min(obt, (p.xyz - ogn) * s);
	}

	float3 orbitMinAbs(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return min(obt, abs((p.xyz - ogn) * s));
	}

	float3 orbitMax(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return max(obt, (p.xyz - ogn) * s);
	}

	float3 orbitMaxAbs(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return max(obt, abs((p.xyz - ogn) * s));
	}

	float3 orbitSum(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return obt + (p.xyz - ogn) * s;
	}

	float3 orbitSumAbs(float3 obt, float3 p, float3 s, float3 ogn)
	{
		return obt + abs((p.xyz - ogn) * s);
	}
	//##########################################
	//   Pupsik space folding functions

	float4 planeFold( float4 z, float3 n, float d) {
		z.xyz -= 2.0 * min(0.0, dot(z.xyz, n) - d) * n;

		return z;
	}
	float4 absFold( float4 z, float3 c) {
		z.xyz = abs(z.xyz - c) + c;

		return z;
	}
	float4 sierpinskiFold( float4 z) {
		z.xy -= min(z.x + z.y, 0.0);
		z.xz -= min(z.x + z.z, 0.0);
		z.yz -= min(z.y + z.z, 0.0);

		return z;
	}
	float4 mengerFold( float4 z) {
		float a = min(z.x - z.y, 0.0);
		z.x -= a;
		z.y += a;
		a = min(z.x - z.z, 0.0);
		z.x -= a;
		z.z += a;
		a = min(z.y - z.z, 0.0);
		z.y -= a;
		z.z += a;

		return z;
	}
	float4 sphereFold( float4 z, float minR, float maxR) {
		float r2 = dot(z.xyz, z.xyz);
		z *= max(maxR / max(minR, r2), 1.0);

		return z;
	}
	float4 boxFold( float4 z, float3 r) {
		z.xyz = clamp(z.xyz, -r, r) * 2.0 - z.xyz;

		return z;
	}
	float4 rotXFold( float4 z, float s, float c) {
		z.yz = float2(c*z.y + s*z.z, c*z.z - s*z.y);

		return z;
	}
	float4 rotYFold( float4 z, float s, float c) {
		z.xz = float2(c*z.x - s*z.z, c*z.z + s*z.x);

		return z;
	}
	float4 rotZFold( float4 z, float s, float c) {
		z.xy = float2(c*z.x + s*z.y, c*z.y - s*z.x);

		return z;
	}
	float4 rotXFold( float4 z, float a) {
		z = rotXFold(z, sin(a), cos(a));

		return z;
	}
	float4 rotYFold( float4 z, float a) {
		z = rotYFold(z, sin(a), cos(a));

		return z;
	}
	float4 rotZFold( float4 z, float a) {
		z = rotZFold(z, sin(a), cos(a));

		return z;
	}

	float4 scaleTranslateFold(float4 z, float s, float3 t)
	{
		z *= s;
		z.xyz += t;

		return z;
	}

	float4 scaleOriginFold(float4 z, float s)
	{
		z *= s;

		return z;
	}

	//##########################################
	//   Pupsik primitive distance estimators

	float sdCone(float4 p, float2 c, float h )
	{
	  // c is the sin/cos of the angle, h is height
	  // Alternatively pass q instead of (c,h),
	  // which is the point at the base in 2D
	  float2 q = h*float2(c.x/c.y,-1.0);
	    
	  float2 w = float2( length(p.xz), p.y );
	  float2 a = w - q*clamp( dot(w,q)/dot(q,q), 0.0, 1.0 );
	  float2 b = w - q*float2( clamp( w.x/q.x, 0.0, 1.0 ), 1.0 );
	  float k = sign( q.y );
	  float d = min(dot( a, a ),dot(b, b));
	  float s = max( k*(w.x*q.y-w.y*q.x),k*(w.y-q.y)  );
	  return sqrt(d)*sign(s) / p.w;
	}

	float sdHexPrism( float4 p, float2 h )
	{
	  const float3 k = float3(-0.8660254, 0.5, 0.57735);
	  p = abs(p);
	  p.xy -= 2.0*min(dot(k.xy, p.xy), 0.0)*k.xy;
	  float2 d = float2(
	       length(p.xy-float2(clamp(p.x,-k.z*h.x,k.z*h.x), h.x))*sign(p.y-h.x),
	       p.z-h.y );
	  return (min(max(d.x,d.y),0.0) + length(max(d,0.0))) / p.w;
	}

	float sdSphere(float4 p, float r) {
		return (length(p.xyz) - r) / p.w;
	}

	float sdCapsule( float4 p, float h, float r )
	{
	  p.y -= clamp( p.y, 0.0, h );
	  return (length( p ) - r) / p.w;
	}

	float sdTorus( float4 p, float2 t )
	{
	  float2 q = float2(length(p.xz)-t.x,p.y);
	  return (length(q)-t.y) / p.w;
	}

	float sdBox(float4 p, float3 s) {
		float3 q = abs(p.xyz) - s;
		return (min(max(max(q.y, q.z), q.x), 0.0) + length(max(q, 0.0))) / p.w;
	}
	float sdTetrahedron(float4 p, float r) {
		float md = max(max(-p.x - p.y - p.z, p.x + p.y - p.z),
					   max(-p.x + p.y + p.z, p.x - p.y + p.z));
		return (md - r) / (p.w * sqrt(3.0));
	}
	float sdInfCross(float4 p, float r) {
		float3 q = p.xyz * p.xyz;
		return (sqrt(min(min(q.x + q.y, q.x + q.z), q.y + q.z)) - r) / p.w;
	}
	float sdInfCrossXY(float4 p, float r) {
		float3 q = p.xyz * p.xyz;
		return (sqrt(min(q.x, q.y) + q.z) - r) / p.w;
	}
	float sdInfLine(float4 p, float3 n, float r) {
		return (length(p.xyz - n*dot(p.xyz, n)) - r) / p.w;
	}

	//##########################################
	//   Quaternions operations

	// quaternion^2
	float4 qSqr(float4 a )
	{
	    return float4( a.x*a.x - a.y*a.y - a.z*a.z - a.w*a.w,
	                 2.0*a.x*a.y,
	                 2.0*a.x*a.z,
	                 2.0*a.x*a.w );
	}

	// quaternion^3
	float4 qCube( float4 a )
	{
		return a * ( 4.0*a.x*a.x - dot(a,a)*float4(3.0,1.0,1.0,1.0) );
	}

	// quaternion multiplication
	float4 qmul(float4 a, float4 b)
	{
	    return float4(
	        a.x * b.x - a.y * b.y - a.z * b.z - a.w * b.w,
	        a.y * b.x + a.x * b.y + a.z * b.w - a.w * b.z, 
	        a.z * b.x + a.x * b.z + a.w * b.y - a.y * b.w,
	        a.w * b.x + a.x * b.w + a.y * b.z - a.z * b.y );

	}
	float4 qconj(float4 a)
	{
	    return float4( a.x, -a.yzw );
	}
	float qlength2(float4 q)
	{
	    return dot(q,q);
	}
	float lengthSquared( float4 z ) { return dot(z,z); }

	//##########################################

	int   seed;

	float2 sdMondelbrot(float3 p, out float3 outputColor) {
		float3 z = p;
		float dr = 1.0;
		float r = 0.0;

		float i = 0;
		float t0 = 1.0;

		for (i = 0; i < Iterations ; i+=1.0f) {
			r = length(z);

			if (r > Bailout) {
	            break;
	        }

			// convert to polar coordinates
			float theta = asin( z.z/r );
			float phi = atan2( z.y,z.x );

			float ct = cos(theta);
			float st = sin(theta);
			float cp = cos(phi);
			float sp = sin(phi);

			dr =  pow(r, Power - 1.0f) * Power * dr + FoldOffset.x;

			// scale and rotate the point
			float zr = pow(r, Power);
			theta = theta * Power + 25.0f * Time;
			phi = phi * Power;
			
			// convert back to cartesian coordinates
			z = zr * float3( cos(theta)*cos(phi), cos(theta)*sin(phi), sin(theta) );
			z += p;

			t0 = min(t0, zr);
		}
		float d = 0.5 * log(r) * r / dr;
		outputColor = colorSurface(float2(d, i)) * Orbit + OrbitOrigin;

	    return float2(d, t0);
	}

	float2 sdJulia(float3 p, out float3 outputColor)
	{
		float4 z = float4(p,0.0);
	    float md2 = 1.0;
	    float mz2 = dot(z,z);
		float n = 0.0f;

	    float4 trap = float4(abs(z.xyz),dot(z,z));
	    for( int i=0; i < Iterations; i++ )
	    {
	        md2 *= 4.0 * mz2;
	        z = qSqr(z) + float4(FoldOffset, FoldOffset.x);  

	        trap = min(trap, float4(abs(z.xyz),dot(z,z)) );

	        mz2 = qlength2(z);

	        if(mz2 > Bailout) break;
			n += 1.0f;
	    }
		float d = 0.25*sqrt(mz2/md2)*log(mz2);
		outputColor = colorSurface(float2(d, n)) * Orbit + OrbitOrigin;

		return float2(d, length(trap));
	}

	float3 colorSurface(float2 tn )
	{
	    float3 col = 0.5+0.5*cos(log2(tn.y)*0.9+3.5+float3(0.0,0.6,1.0));
	    float inside = smoothstep(14.0,15.0,tn.y);
	    col *= float3(0.45,0.42,0.40) + float3(0.55,0.58,0.60)*inside;
	    col = lerp(col*col*(3.0-2.0*col),col,inside);
	    col = lerp( lerp(col,float3(1.0, 1.0f, 1.0f) * dot(col,float3(0.3333, 0.3333, 0.3333)),-0.4),
	                        col, inside);
	    return clamp(col*0.65,0.0,1.0);
	}

	float2 sdJulia2(float3 p, out float3 outputColor)
	{
		float4  kC = float4(-2,6,15,-6)/22.0;
	    float4 z = float4( p, 0.0 );
	    float dz2 = 1.0;
		float m2  = 0.0;
	    float n   = 0.0;

	    for( int i=0; i<Iterations; i++ ) 
		{
			dz2 *= 9.0*lengthSquared(qSqr(z));
			z = qCube( z ) + float4(FoldOffset, -6.0 / 22.0);
	        
			m2 = lengthSquared(z);

	        if( m2>Bailout ) break;				 
			n += 1.0;
		}
	   
		float d = 0.25*log(m2)*sqrt(m2/dz2);

		outputColor = colorSurface(float2(d, n)) * Orbit + OrbitOrigin;

		return float2(d, n);        
	}

	float3x3 pupsik_euler_yzx_to_axis_angle(float3 rot)
	{
		float x = rot.x;
		float y = rot.y;
		float z = rot.z;
	    float3 r1 = float3(cos(y) * cos(z), - cos(y) * sin(z), sin(y));
	    float3 r2 = float3(cos(x) * sin(z) + cos(z) * sin(x) * sin(y), cos(x) * cos(z) - sin(x) * sin(y) * sin(z), -cos(y) * sin(x));
	    float3 r3 = float3(sin(x) * sin(z) - cos(x) * cos(z) * sin(y), cos(z) * sin(x) + cos(x) * sin(y) * sin(z), cos(x) * cos(y));

		return float3x3(r3, r2, r1);
	}

	float pupsik_opSmoothUnion( float d1, float d2, float k ) 
	{
		float h = clamp( 0.5 + 0.5*(d2-d1)/k, 0.0, 1.0 );
		return lerp( d2, d1, h ) - k*h*(1.0-h);
	}
	
	float pupsik_opSmoothSubtraction( float d1, float d2, float k ) 
	{
		float h = clamp( 0.5 - 0.5*(d2+d1)/k, 0.0, 1.0 );
		return lerp( d2, -d1, h ) + k*h*(1.0-h); 
	}
	
	float pupsik_opSmoothIntersection( float d1, float d2, float k )
	{
		float h = clamp( 0.5 - 0.5*(d2-d1)/k, 0.0, 1.0 );
		return lerp( d2, d1, h ) + k*h*(1.0-h); 
	}
};
